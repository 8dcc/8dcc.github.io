#+TITLE: Inspecting xorpd's assembly snippets
#+AUTHOR: 8dcc
#+STARTUP: nofold
#+HTML_HEAD: <link rel="icon" type="image/x-icon" href="../img/favicon.png" />
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="../css/main.css" />
#+HTML_LINK_UP: index.html
#+HTML_LINK_HOME: ../index.html

* Introduction
:PROPERTIES:
:CUSTOM_ID: introduction
:END:

This is an analysis of each assembly snippet from the book "[[https://www.xorpd.net/pages/xchg_rax/snip_00.html][xchg rax, rax]]" by
[[https://www.xorpd.net/][xorpd]]. The book consists of 64 very original assembly snippets. It can be bought
physically for a low price and it can be a good gift for some assembly nerd you
might know.

If you want to try and figure out the snippets for yourself, I recommend you
have the following manuals around:

- Intel Software Developer Manual (SDM), specially volume 2. See the [[file:../reference/index.org::#intel-manuals][Intel SDM]]
  section on the reference index for more information.
- The [[https://www.nasm.us/doc/][NASM manual]], since it's the syntax used by the author of the book.

* Snippets
:PROPERTIES:
:CUSTOM_ID: snippets
:END:

Each subsection will correspond to a different snippet from the book.

I took the liberty of modifying some indentation/spacing to fit my own
preferences.

** Snippet 00
:PROPERTIES:
:CUSTOM_ID: snippet-00
:END:

Different ways of setting a register to zero.

#+begin_src nasm
    xor      eax, eax
    lea      rbx, [0]
    loop     $
    mov      rdx, 0
    and      esi, 0
    sub      edi, edi
    push     0
    pop      rbp
#+end_src

Note how =[0]= doesn't refer to the value at address 0, it simply indicates that 0
is an /effective address/. Some instructions, like =mov=, operate on the value at
the address (effectively "dereferencing" it); but some others, like =lea=, operate
on the address itself, loading it into the specified register. See section 3.3
(Effective Addresses) of the [[https://www.nasm.us/xdoc/2.14.02/html/nasmdoc3.html#section-3.3][NASM manual]].

The =loop= instruction decrements the count register (=rcx=) and, if it's not zero,
jumps to the target operand. In this case, the operand is =$=, which in NASM
refers to the address of the current instruction. See section 3.5 (Expressions)
of the [[https://www.nasm.us/doc/nasmdoc3.html#section-3.5][NASM manual]].

** Snippet 01
:PROPERTIES:
:CUSTOM_ID: snippet-01
:END:

Calculate [[https://en.wikipedia.org/wiki/Fibonacci_sequence][Fibonacci]] or [[https://en.wikipedia.org/wiki/Lucas_number][Lucas]] sequences.

#+begin_src nasm
.loop:
    xadd     rax, rdx
    loop     .loop
#+end_src

The =xadd= instruction exchanges values in the first and second operand (like
=xchg=) and stores the sum of the two values in the first operand. In this case it
will exchange the values at =rax= and =rdx=, and save the sum in =rax=. Because of the
=loop= instruction, this process will be repeated =rcx= times.

Using the =xadd= instruction on the same registers over and over can be used to
generate Fibonacci or similar sequences. For example:

| Iteration | =rax= | =rdx= |
|-----------+-----+-----+
|         0 |   1 |   0 |
|         1 |   1 |   1 |
|         2 |   2 |   1 |
|         3 |   3 |   2 |
|         4 |   5 |   3 |

As you can see, the value in each register is the sum of the values that were in
that register in the previous two iterations.

By changing the initial state of the registers, the Lucas sequence can be
generated:

| Sequence  | Initial =rax= | Initial =rdx= |
|-----------+-------------+-------------|
| Fibonacci |           1 |           0 |
| Lucas     |           1 |           2 |

** Snippet 02
:PROPERTIES:
:CUSTOM_ID: snippet-02
:END:

The following code converts non-zero numbers to one.

#+begin_src nasm
    neg      rax
    sbb      rax, rax
    neg      rax
#+end_src

This is equivalent to the following C code:

#+begin_src C
int val = 123;
bool is_non_zero = !!val; /* 1 or 0 */
#+end_src

There are a few key details for understanding the assembly snippet.

The =neg= instruction replaces the value of the operand with its [[https://en.wikipedia.org/wiki/Two%27s_complement][two's
complement]]. This is equivalent to subtracting the value from zero. It's
important to note that this instruction also sets the carry flag (=CF=) if the
source operand was non-zero.

The =sbb= instruction adds the carry flag (=CF=) to the source operand, and
subtracts the result from the destination operand. The result is stored into the
destination operand. The operation is:

#+NAME: example1
#+begin_example text
dst = dst - (src + CF)
#+end_example

Note how the carry flag (=CF=) is /only/ added to the source operand. This happens
even if both source and operand registers are the same.

Let's look at the operations of the assembly snippet step by step.

| Step | Last operation       | ~rax~ = 0       | ~rax~ = 1       | ~rax~ = 5        |
|------+----------------------+---------------+---------------+----------------|
|    1 | Initial state        | ~0b0000...0000~ | ~0b0000...0001~ | ~0b0000...00101~ |
|    2 | First ~neg~            | ~0b0000...0000~ | ~0b1111...1111~ | ~0b1111...11011~ |
|    3 | Add carry flag (~sbb~) | ~0b0000...0000~ | ~0b0000...0000~ | ~0b1111...11100~ |
|    4 | Subtract (~sbb~)       | ~0b0000...0000~ | ~0b1111...1111~ | ~0b1111...11111~ |
|    5 | Second ~neg~           | ~0b0000...0000~ | ~0b1111...1111~ | ~0b0000...00000~ |

Again, note that the 4th step isn't subtracting the number of the 3rd step to
itself; it is subtracting it to the number of the 2nd step.

Since the source and destination operands of the =sbb= instruction are the same,
it's essentially setting the operand to the negated carry flag (=CF=):

#+NAME: example2
#+begin_example
x = x - (x + CF)
x = -(CF)
#+end_example

After negating this value with the second =neg= instruction, the operand becomes
the carry flag (=CF=), which was set by the first =neg= instruction to either 0 or 1
depending on whether the value was zero or non-zero, respectively.

As we can see, non-zero values are set to one, while zero remains unchanged.

* COMMENT TODO
** Snippet 03
:PROPERTIES:
:CUSTOM_ID: snippet-03
:END:

#+begin_src nasm
    sub      rdx, rax
    sbb      rcx, rcx
    and      rcx, rdx
    add      rax, rcx
#+end_src

** Snippet 04
:PROPERTIES:
:CUSTOM_ID: snippet-04
:END:

#+begin_src nasm
    xor      al, 0x20
#+end_src

** Snippet 05
:PROPERTIES:
:CUSTOM_ID: snippet-05
:END:

#+begin_src nasm
    sub      rax, 5
    cmp      rax, 4
#+end_src

** Snippet 06
:PROPERTIES:
:CUSTOM_ID: snippet-06
:END:

#+begin_src nasm
    not      rax
    inc      rax
    neg      rax
#+end_src

** Snippet 07
:PROPERTIES:
:CUSTOM_ID: snippet-07
:END:

#+begin_src nasm
    inc      rax
    neg      rax
    inc      rax
    neg      rax
#+end_src

** Snippet 08
:PROPERTIES:
:CUSTOM_ID: snippet-08
:END:

#+begin_src nasm
    add      rax, rdx
    rcr      rax, 1
#+end_src

** Snippet 09
:PROPERTIES:
:CUSTOM_ID: snippet-09
:END:

#+begin_src nasm
    shr      rax, 3
    adc      rax, 0
#+end_src

** Snippet 0A
:PROPERTIES:
:CUSTOM_ID: snippet-0a
:END:

#+begin_src nasm
    add      byte [rdi], 1
.loop:
    inc      rdi
    adc      byte [rdi], 0
    loop     .loop
#+end_src

** Snippet 0B
:PROPERTIES:
:CUSTOM_ID: snippet-0b
:END:

#+begin_src nasm
    not      rdx
    neg      rax
    sbb      rdx, -1
#+end_src

** Snippet 0C
:PROPERTIES:
:CUSTOM_ID: snippet-0c
:END:

#+begin_src nasm
    mov      rcx, rax
    xor      rcx, rbx
    ror      rcx, 0xd

    ror      rax, 0xd
    ror      rbx, 0xd
    xor      rax, rbx

    cmp      rax, rcx
#+end_src

** Snippet 0D
:PROPERTIES:
:CUSTOM_ID: snippet-0d
:END:

#+begin_src nasm
    mov      rdx, rbx

    xor      rbx, rcx
    and      rbx, rax

    and      rdx, rax
    and      rax, rcx
    xor      rax, rdx

    cmp      rax, rbx
#+end_src

** Snippet 0E
:PROPERTIES:
:CUSTOM_ID: snippet-0e
:END:

#+begin_src nasm
    mov      rcx, rax
    and      rcx, rbx
    not      rcx

    not      rax
    not      rbx
    or       rax, rbx

    cmp      rax, rcx
#+end_src

** Snippet 0F
:PROPERTIES:
:CUSTOM_ID: snippet-0f
:END:

#+begin_src nasm
.loop:
    xor      byte [rsi], al
    lodsb
    loop     .loop
#+end_src

** Snippet 10
:PROPERTIES:
:CUSTOM_ID: snippet-10
:END:

#+begin_src nasm
    push     rax
    push     rcx
    pop      rax
    pop      rcx

    xor      rax, rcx
    xor      rcx, rax
    xor      rax, rcx

    add      rax, rcx
    sub      rcx, rax
    add      rax, rcx
    neg      rcx

    xchg     rax, rcx
#+end_src

** Snippet 11
:PROPERTIES:
:CUSTOM_ID: snippet-11
:END:

#+begin_src nasm
.loop:
    mov      dl, byte [rsi]
    xor      dl, byte [rdi]
    inc      rsi
    inc      rdi
    or       al, dl
    loop     .loop
#+end_src

** Snippet 12
:PROPERTIES:
:CUSTOM_ID: snippet-12
:END:

#+begin_src nasm
    mov      rcx, rdx
    and      rdx, rax
    or       rax, rcx
    add      rax, rdx
#+end_src

** Snippet 13
:PROPERTIES:
:CUSTOM_ID: snippet-13
:END:

#+begin_src nasm
    mov      rcx, 0x40
.loop:
    mov      rdx, rax
    xor      rax, rbx
    and      rbx, rdx
    shl      rbx, 0x1
    loop     .loop
#+end_src

** Snippet 14
:PROPERTIES:
:CUSTOM_ID: snippet-14
:END:

#+begin_src nasm
    mov      rcx, rax
    and      rcx, rdx

    xor      rax, rdx
    shr      rax, 1

    add      rax, rcx
#+end_src

** Snippet 15
:PROPERTIES:
:CUSTOM_ID: snippet-15
:END:

#+begin_src nasm
    mov      rdx, 0xffffffff80000000
    add      rax, rdx
    xor      rax, rdx
#+end_src

** Snippet 16
:PROPERTIES:
:CUSTOM_ID: snippet-16
:END:

#+begin_src nasm
    xor      rax, rbx
    xor      rbx, rcx
    mov      rsi, rax
    add      rsi, rbx
    cmovc    rax, rbx
    xor      rax, rbx
    cmp      rax, rsi
#+end_src

** Snippet 17
:PROPERTIES:
:CUSTOM_ID: snippet-17
:END:

#+begin_src nasm
    cqo
    xor      rax, rdx
    sub      rax, rdx
#+end_src

** Snippet 18
:PROPERTIES:
:CUSTOM_ID: snippet-18
:END:

#+begin_src nasm
    rdtsc
    shl      rdx, 0x20
    or       rax, rdx
    mov      rcx, rax

    rdtsc
    shl      rdx, 0x20
    or       rax, rdx

    cmp      rcx, rax
#+end_src

** Snippet 19
:PROPERTIES:
:CUSTOM_ID: snippet-19
:END:

#+begin_src nasm
    call     .skip
    db       'hello world!', 0
.skip:
    call     printf  ; print_str
    add      rsp, 8
#+end_src

** Snippet 1A
:PROPERTIES:
:CUSTOM_ID: snippet-1a
:END:

#+begin_src nasm
        call     .next
    .next:
        pop      rax
#+end_src

** Snippet 1B
:PROPERTIES:
:CUSTOM_ID: snippet-1b
:END:

#+begin_src nasm
    push     rax
    ret
#+end_src

** Snippet 1C
:PROPERTIES:
:CUSTOM_ID: snippet-1c
:END:

#+begin_src nasm
    pop      rsp
#+end_src

** Snippet 1D
:PROPERTIES:
:CUSTOM_ID: snippet-1d
:END:

#+begin_src nasm
    mov      rsp, buff2 + N*8 + 8
    mov      rbp, buff1 + N*8
    enter    0, N+1
#+end_src

** Snippet 1E
:PROPERTIES:
:CUSTOM_ID: snippet-1e
:END:

#+begin_src nasm
    ; NOTE: The `das' instruction is not valid in 64-bit mode
    cmp      al, 0x0a
    sbb      al, 0x69
    das
#+end_src

** Snippet 1F
:PROPERTIES:
:CUSTOM_ID: snippet-1f
:END:

#+begin_src nasm
.loop:
    bsf      rcx, rax
    shr      rax, cl
    cmp      rax, 1
    je       .exit_loop
    lea      rax, [rax + 2*rax + 1]
    jmp      .loop
.exit_loop:
#+end_src

** Snippet 20
:PROPERTIES:
:CUSTOM_ID: snippet-20
:END:

#+begin_src nasm
    mov      rcx, rax
    shl      rcx, 2
    add      rcx, rax
    shl      rcx, 3
    add      rcx, rax
    shl      rcx, 1
    add      rcx, rax
    shl      rcx, 1
    add      rcx, rax
    shl      rcx, 3
    add      rcx, rax
#+end_src

** Snippet 21
:PROPERTIES:
:CUSTOM_ID: snippet-21
:END:

#+begin_src nasm
    mov      rsi, rax
    add      rax, rbx
    mov      rdi, rdx
    sub      rdx, rcx
    add      rdi, rcx

    imul     rax, rcx
    imul     rsi, rdx
    imul     rdi, rbx

    add      rsi, rax
    mov      rbx, rsi
    sub      rax, rdi
#+end_src

** Snippet 22
:PROPERTIES:
:CUSTOM_ID: snippet-22
:END:

#+begin_src nasm
    mov      rdx, 0xaaaaaaaaaaaaaaab
    mul      rdx
    shr      rdx, 1
    mov      rax, rdx
#+end_src

** Snippet 23
:PROPERTIES:
:CUSTOM_ID: snippet-23
:END:

#+begin_src nasm
.loop:
    cmp      rax, 5
    jbe      .exit_loop
    mov      rdx, rax
    shr      rdx, 2
    and      rax, 3
    add      rax, rdx
    jmp      .loop
.exit_loop:

    cmp      rax, 3
    cmc
    sbb      rdx, rdx
    and      rdx, 3
    sub      rax, rdx
#+end_src

** Snippet 24
:PROPERTIES:
:CUSTOM_ID: snippet-24
:END:

#+begin_src nasm
    mov      rbx, rax
    mov      rsi, rax
.loop:
    mul      rbx
    mov      rcx, rax

    sub      rax, 2
    neg      rax
    mul      rsi
    mov      rsi, rax

    cmp      rcx, 1
    ja       .loop
.exit_loop:
#+end_src

** Snippet 25
:PROPERTIES:
:CUSTOM_ID: snippet-25
:END:

#+begin_src nasm
    xor      eax, eax
    mov      rcx, 1
    shl      rcx, 0x20
.loop:
    movzx    rbx, cx
    imul     rbx, rbx

    ror      rcx, 0x10
    movzx    rdx, cx
    imul     rdx, rdx
    rol      rcx, 0x10

    add      rbx, rdx
    shr      rbx, 0x20
    cmp      rbx, 1
    adc      rax, 0
    loop     .loop
#+end_src

** Snippet 26
:PROPERTIES:
:CUSTOM_ID: snippet-26
:END:

#+begin_src nasm
    mov      rdx, rax
    shr      rax, 7
    shl      rdx, 0x39
    or       rax, rdx
#+end_src

** Snippet 27
:PROPERTIES:
:CUSTOM_ID: snippet-27
:END:

#+begin_src nasm
    mov      ch, cl
    inc      ch
    shr      ch, 1
    shr      cl, 1
    shr      rax, cl
    xchg     ch, cl
    shr      rax, cl
#+end_src

** Snippet 28
:PROPERTIES:
:CUSTOM_ID: snippet-28
:END:

#+begin_src nasm
    clc
.loop:
    rcr      byte [rsi], 1
    inc      rsi
    loop     .loop
#+end_src

** Snippet 29
:PROPERTIES:
:CUSTOM_ID: snippet-29
:END:

#+begin_src nasm
    lea      rdi, [rsi + 3]
    rep movsb
#+end_src

** Snippet 2A
:PROPERTIES:
:CUSTOM_ID: snippet-2a
:END:

#+begin_src nasm
    mov      rsi, rbx
    mov      rdi, rbx
.loop:
    lodsq
    xchg     rax, qword [rbx]
    stosq
    loop     .loop
#+end_src

** Snippet 2B
:PROPERTIES:
:CUSTOM_ID: snippet-2b
:END:

#+begin_src nasm
    xor      eax, eax
    xor      edx, edx
.loop1:
    xlatb
    xchg     rax, rdx
    xlatb
    xlatb
    xchg     rax, rdx
    cmp      al, dl
    jnz      .loop1

    xor      eax, eax
.loop2:
    xlatb
    xchg     rax, rdx
    xlatb
    xchg     rax, rdx
    cmp      al, dl
    jnz      .loop2
#+end_src

** Snippet 2C
:PROPERTIES:
:CUSTOM_ID: snippet-2c
:END:

#+begin_src nasm
    mov      qword [rbx + 8*rcx], 0
    mov      qword [rbx + 8*rdx], 1
    mov      rax, qword [rbx + 8*rcx]

    mov      qword [rbx], rsi
    mov      qword [rbx + 8], rdi
    mov      rax, qword [rbx + 8*rax]
#+end_src

** Snippet 2D
:PROPERTIES:
:CUSTOM_ID: snippet-2d
:END:

#+begin_src nasm
    mov      rdx, rax
    dec      rax
    and      rax, rdx
#+end_src

** Snippet 2E
:PROPERTIES:
:CUSTOM_ID: snippet-2e
:END:

#+begin_src nasm
    mov      rdx, rax
    dec      rdx
    xor      rax, rdx
    shr      rax, 1
    cmp      rax, rdx
#+end_src

** Snippet 2F
:PROPERTIES:
:CUSTOM_ID: snippet-2f
:END:

#+begin_src nasm
        xor      eax, eax
    .loop:
        jrcxz    .exit_loop
        inc      rax
        mov      rdx, rcx
        dec      rdx
        and      rcx, rdx
        jmp      .loop
    .exit_loop:
#+end_src

** Snippet 30
:PROPERTIES:
:CUSTOM_ID: snippet-30
:END:

#+begin_src nasm
    and      rax, rdx

    sub      rax, rdx
    and      rax, rdx

    dec      rax
    and      rax, rdx
#+end_src

** Snippet 31
:PROPERTIES:
:CUSTOM_ID: snippet-31
:END:

#+begin_src nasm
    mov      rcx, rax
    shr      rcx, 1
    xor      rcx, rax

    inc      rax

    mov      rdx, rax
    shr      rdx, 1
    xor      rdx, rax

    xor      rdx, rcx
#+end_src

** Snippet 32
:PROPERTIES:
:CUSTOM_ID: snippet-32
:END:

#+begin_src nasm
    mov      rcx, rax

    mov      rdx, rax
    shr      rdx, 1
    xor      rax, rdx

    popcnt   rax, rax
    xor      rax, rcx
    and      rax, 1
#+end_src

** Snippet 33
:PROPERTIES:
:CUSTOM_ID: snippet-33
:END:

#+begin_src nasm
    mov      rdx, rax
    shr      rdx, 0x1
    xor      rax, rdx

    mov      rdx, rax
    shr      rdx, 0x2
    xor      rax, rdx

    mov      rdx, rax
    shr      rdx, 0x4
    xor      rax, rdx

    mov      rdx, rax
    shr      rdx, 0x8
    xor      rax, rdx

    mov      rdx, rax
    shr      rdx, 0x10
    xor      rax, rdx

    mov      rdx, rax
    shr      rdx, 0x20
    xor      rax, rdx
#+end_src

** Snippet 34
:PROPERTIES:
:CUSTOM_ID: snippet-34
:END:

#+begin_src nasm
    mov      ecx, eax
    and      ecx, 0xffff0000
    shr      ecx, 0x10
    and      eax, 0x0000ffff
    shl      eax, 0x10
    or       eax, ecx

    mov      ecx, eax
    and      ecx, 0xff00ff00
    shr      ecx, 0x8
    and      eax, 0x00ff00ff
    shl      eax, 0x8
    or       eax, ecx

    mov      ecx, eax
    and      ecx, 0xcccccccc
    shr      ecx, 0x2
    and      eax, 0x33333333
    shl      eax, 0x2
    or       eax, ecx

    mov      ecx, eax
    and      ecx, 0xf0f0f0f0
    shr      ecx, 0x4
    and      eax, 0x0f0f0f0f
    shl      eax, 0x4
    or       eax, ecx

    mov      ecx, eax
    and      ecx, 0xaaaaaaaa
    shr      ecx, 0x1
    and      eax, 0x55555555
    shl      eax, 0x1
    or       eax, ecx
#+end_src

** Snippet 35
:PROPERTIES:
:CUSTOM_ID: snippet-35
:END:

#+begin_src nasm
    mov      edx, eax
    and      eax, 0x55555555
    shr      edx, 0x1
    and      edx, 0x55555555
    add      eax, edx

    mov      edx, eax
    and      eax, 0x33333333
    shr      edx, 0x2
    and      edx, 0x33333333
    add      eax, edx

    mov      edx, eax
    and      eax, 0x0f0f0f0f
    shr      edx, 0x4
    and      edx, 0x0f0f0f0f
    add      eax, edx

    mov      edx, eax
    and      eax, 0x00ff00ff
    shr      edx, 0x8
    and      edx, 0x00ff00ff
    add      eax, edx

    mov      edx, eax
    and      eax, 0x0000ffff
    shr      edx, 0x10
    and      edx, 0x0000ffff
    add      eax, edx
#+end_src

** Snippet 36
:PROPERTIES:
:CUSTOM_ID: snippet-36
:END:

#+begin_src nasm
    dec      rax

    mov      rdx, rax
    shr      rdx, 0x1
    or       rax, rdx

    mov      rdx, rax
    shr      rdx, 0x2
    or       rax, rdx

    mov      rdx, rax
    shr      rdx, 0x4
    or       rax, rdx

    mov      rdx, rax
    shr      rdx, 0x8
    or       rax, rdx

    mov      rdx, rax
    shr      rdx, 0x10
    or       rax, rdx

    mov      rdx, rax
    shr      rdx, 0x20
    or       rax, rdx

    inc      rax
#+end_src

** Snippet 37
:PROPERTIES:
:CUSTOM_ID: snippet-37
:END:

#+begin_src nasm
    mov      rdx, rax
    not      rdx
    mov      rcx, 0x8080808080808080
    and      rdx, rcx
    mov      rcx, 0x0101010101010101
    sub      rax, rcx
    and      rax, rdx
#+end_src

** Snippet 38
:PROPERTIES:
:CUSTOM_ID: snippet-38
:END:

#+begin_src nasm
    bsf      rcx, rax

    mov      rdx, rax
    dec      rdx
    or       rdx, rax

    mov      rax, rdx
    inc      rax

    mov      rbx, rdx
    not      rbx
    inc      rdx
    and      rdx, rbx
    dec      rdx

    shr      rdx, cl
    shr      rdx, 1

    or       rax, rdx
#+end_src

** Snippet 39
:PROPERTIES:
:CUSTOM_ID: snippet-39
:END:

#+begin_src nasm
    mov      rdx, 0xaaaaaaaaaaaaaaaa
    add      rax, rdx
    xor      rax, rdx
#+end_src

** Snippet 3A
:PROPERTIES:
:CUSTOM_ID: snippet-3a
:END:

#+begin_src nasm
    mov      rdx, rax
    neg      rdx
    and      rax, rdx

    mov      rdx, 0x218a392cd3d5dbf
    mul      rdx
    shr      rax, 0x3a

    xlatb
#+end_src

** Snippet 3B
:PROPERTIES:
:CUSTOM_ID: snippet-3b
:END:

#+begin_src nasm
    cdq
    shl      eax, 1
    and      edx, 0xc0000401
    xor      eax, edx
#+end_src

** Snippet 3C
:PROPERTIES:
:CUSTOM_ID: snippet-3c
:END:

#+begin_src nasm
    mov      rbx, rax
    mov      rdx, rbx
    mov      rcx, 0xaaaaaaaaaaaaaaaa
    and      rbx, rcx
    shr      rbx, 1
    and      rbx, rdx
    popcnt   rbx, rbx
    and      rbx, 1

    neg      rax
    mov      rdx, rax
    mov      rcx, 0xaaaaaaaaaaaaaaaa
    and      rax, rcx
    shr      rax, 1
    and      rax, rdx
    popcnt   rax, rax
    and      rax, 1

    mov      rdx, rax
    add      rax, rbx
    dec      rax
    neg      rax
    sub      rdx, rbx
#+end_src

** Snippet 3D
:PROPERTIES:
:CUSTOM_ID: snippet-3d
:END:

#+begin_src nasm
    mov      rcx, 1
.loop:
    xor      rax, rcx
    not      rax
    and      rcx, rax
    not      rax

    xor      rdx, rcx
    not      rdx
    and      rcx, rdx
    not      rdx

    shl      rcx, 1
    jnz      .loop
#+end_src

** Snippet 3E
:PROPERTIES:
:CUSTOM_ID: snippet-3e
:END:

#+begin_src nasm
    mov      rdx, rax
    shr      rdx, 1
    xor      rax, rdx

    popcnt   rax, rax
    and      rax, 0x3
#+end_src

** Snippet 3F
:PROPERTIES:
:CUSTOM_ID: snippet-3f
:END:

#+begin_src nasm
    mov      rbx, 3
    mov      r8, rax
    mov      rcx, rax
    dec      rcx

    and      rax, rcx
    xor      edx, edx
    div      rbx
    mov      rsi, rdx

    mov      rax, r8
    or       rax, rcx
    xor      edx, edx
    div      rbx
    inc      rdx
    cmp      rdx, rbx
    sbb      rdi, rdi
    and      rdi, rdx

    bsf      rax, r8
#+end_src
